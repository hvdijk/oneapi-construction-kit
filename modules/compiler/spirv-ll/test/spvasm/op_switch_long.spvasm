; Copyright (C) Codeplay Software Limited
;
; Licensed under the Apache License, Version 2.0 (the "License") with LLVM
; Exceptions; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     https://github.com/codeplaysoftware/oneapi-construction-kit/blob/main/LICENSE.txt
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
; WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
; License for the specific language governing permissions and limitations
; under the License.
;
; SPDX-License-Identifier: Apache-2.0 WITH LLVM-exception

; RUN: %if online-spirv-as %{ spirv-as --target-env %spv_tgt_env -o %spv_file_s %s %}
; RUN: %if online-spirv-as %{ spirv-val %spv_file_s %}
; RUN: spirv-ll-tool -a Vulkan -c Int64 %spv_file_s | FileCheck %s
               OpCapability Shader
               OpCapability Int64
               OpMemoryModel Logical GLSL450
               OpEntryPoint GLCompute %main "main"
               OpExecutionMode %main LocalSize 1 1 1
               OpSource GLSL 450
               OpName %main "main"
               OpName %cond "cond"
               OpName %a "a"
               OpName %res "res"
       %void = OpTypeVoid
          %3 = OpTypeFunction %void
        %int = OpTypeInt 32 1
       %long = OpTypeInt 64 1
%_ptr_Function_int = OpTypePointer Function %int
%_ptr_Function_long = OpTypePointer Function %long
     %int_42 = OpConstant %int 42
     %int_24 = OpConstant %int 24
    %long_42 = OpConstant %long 42
       %main = OpFunction %void None %3
          %5 = OpLabel
       %cond = OpVariable %_ptr_Function_long Function
          %a = OpVariable %_ptr_Function_int Function
        %res = OpVariable %_ptr_Function_int Function
               OpStore %cond %long_42
         %10 = OpLoad %long %cond
               OpSelectionMerge %13 None
; Switch on a large value that will be incorrect if the full width of the literal isn't read.
               OpSwitch %10 %12 -540431955301232641 %11
         %12 = OpLabel
               OpStore %a %int_24
               OpBranch %13
         %11 = OpLabel
               OpStore %a %int_42
               OpBranch %13
         %13 = OpLabel
         %19 = OpLoad %int %a
         %20 = OpBitwiseXor %int %19 %int_42
               OpStore %res %20
               OpReturn
               OpFunctionEnd
; CHECK: define spir_kernel void @main()
; CHECK: [[COND:%.*]] = alloca i64
; CHECK: [[A:%.*]] = alloca i32
; CHECK: store i64 42, ptr [[COND]]
; CHECK: [[LD_COND:%.*]] = load i64, ptr [[COND]]
; CHECK: switch i64 [[LD_COND]], label %[[DEFAULT:[0-9]]] [
; CHECK: i64 -540431955301232641, label %[[CASE:[0-9]]]
; CHECK: [[DEFAULT]]:
; CHECK: store i32 24, ptr [[A]]
; CHECK: br label [[END:%.*]]
; CHECK: [[CASE]]:
; CHECK: store i32 42, ptr [[A]]
; CHECK: br label [[END]]
; CHECK: [[LD_A:%.*]] = load i32, ptr [[A]]
; CHECK: [[XOR_A:%.*]] = xor i32 [[LD_A]], 42
; CHECK: store i32 [[XOR_A]], ptr {{%.*}}
; CHECK ret void
